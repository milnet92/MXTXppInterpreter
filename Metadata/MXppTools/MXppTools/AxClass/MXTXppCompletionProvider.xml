<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>MXTXppCompletionProvider</Name>
	<SourceCode>
		<Declaration><![CDATA[
using XppInterpreter.Parser.Metadata;
using Reflection = System.Reflection;

public final class MXTXppCompletionProvider implements ICompletionProvider
{
    private static List classNames, tableNames, edtNames, enumNames, globalFunctions;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>getMethodDocumentation</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the <c>SysDictMethod</c> documentation string
    /// </summary>
    /// <param name = "_method">SysDictMethod</param>
    /// <returns>Method documentation</returns>
    private str getMethodDocumentation(SysDictMethod _method)
    {
        return _method.metadataSyntax();
    }

]]></Source>
			</Method>
			<Method>
				<Name>isValidFieldName</Name>
				<Source><![CDATA[
    /// <summary>
    /// Checks if a name is valid for a field name
    /// </summary>
    /// <param name = "_name">Name</param>
    /// <returns>True if valid, otherwise false</returns>
    private static boolean isValidFieldName(str _name)
    {
        return !strStartsWith(_name, "_") && !strStartsWith(_name, "$");
    }

]]></Source>
			</Method>
			<Method>
				<Name>GetTableMethodCompletions</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the table methods for a table name
    /// </summary>
    /// <param name = "_tableName">Table name</param>
    /// <param name = "_isStatic">If the methods requestes are static</param>
    /// <returns>Colleciton of table method names</returns>
    CompletionCollection GetTableMethodCompletions(str _tableName, boolean _isStatic)
    {
        CompletionCollection completions = new CompletionCollection();

        SysDictTable dictTable = SysDictTable::newName(_tableName);

        if (!dictTable) return completions;

        this.fillMethodCompletions(dictTable.methods(!_isStatic, _isStatic, true), true, completions);

        return completions;
    }

]]></Source>
			</Method>
			<Method>
				<Name>GetTableFieldsCompletions</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the table fields of the given table name
    /// </summary>
    /// <param name = "_tableName">Table name</param>
    /// <returns>Collection of field table names</returns>
    CompletionCollection GetTableFieldsCompletions(str _tableName)
    {
        CompletionCollection completions = new CompletionCollection();

        SysDictTable dictTable = SysDictTable::newName(_tableName);

        if (!dictTable) return completions;

        FieldId      fieldId = dictTable.fieldNext(0, TableScope::IncludeBaseTables);
        SysDictField dictField;

        while (fieldId)
        {
            dictField = new SysDictField(dictTable.id(), fieldId);

            Completion completion = new Completion(dictField.name(), dictField.name(), CompletionEntryType::TableField);
            completion.DocHtml = '<span>' + dictField.name() + " " + dictField.typeName() + '</span>';

            completions.Add(completion);

            fieldId = dictTable.fieldNext(fieldId, TableScope::IncludeBaseTables);
        }

        return completions;
    }

]]></Source>
			</Method>
			<Method>
				<Name>GetClassMethodCompletions</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the methods of a class name
    /// </summary>
    /// <param name = "_className">Class name</param>
    /// <param name = "_isStatic">If the methods are static</param>
    /// <returns>Collection of class methods</returns>
    CompletionCollection GetClassMethodCompletions(str _className, boolean _isStatic)
    {
        CompletionCollection completions = new CompletionCollection();

        SysDictClass dictClass = SysDictClass::newName(_className);

        if (dictClass)
        {
            this.fillMethodCompletions(dictClass.methods(!_isStatic, _isStatic, true), false, completions);
        }
        else
        {
            System.Type type = Microsoft.Dynamics.Ax.Xpp.ReflectionCallHelper::GetTypeFromClassName(_className);

            if (type)
            {
                this.fillNetMethodCompletions(type, _isStatic, completions);
            }
        }

        return completions;
    }

]]></Source>
			</Method>
			<Method>
				<Name>GetClassFieldCompletions</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the fields / properties of a class name
    /// </summary>
    /// <param name = "_className">Class name</param>
    /// <param name = "_isStatic">If the methods are static</param>
    /// <returns>Collection of fields / properties</returns>
    CompletionCollection GetClassFieldCompletions(str _className, boolean _isStatic)
    {
        CompletionCollection completions = new CompletionCollection();

        System.Type type =  Microsoft.Dynamics.Ax.Xpp.ReflectionCallHelper::GetTypeFromClassName(_className);

        if (type == null) return completions;

        // Construct binding flags
        Reflection.BindingFlags flags = Reflection.BindingFlags::Public;

        flags = flags | (_isStatic ? Reflection.BindingFlags::Static : Reflection.BindingFlags::Instance);
        
        var tmpFields = type.GetFields();
        var tmpProps = type.GetProperties();

        var fields = type.GetFields(flags);
        for (int i = 0; i < fields.Length; i++)
        {
            Reflection.FieldInfo fieldInfo = fields.Get(i);
            
            // Skip kernel fields
            if (!MXTXppCompletionProvider::isValidFieldName(fieldInfo.Name) ||
                fieldInfo.DeclaringType.Name == "XppObjectBase")
            {
                continue;
            }

            Completion completion = new Completion(fieldInfo.Name, fieldInfo.Name, CompletionEntryType::ClassProperty);
            Completion.DocHtml = '<span>' + fieldInfo.FieldType.Name + " " + fieldInfo.Name + '</span>';

            completions.Add(completion);
        }

        var properties = type.GetProperties(flags);
        for (int i = 0; i < properties.Length; i++)
        {
            Reflection.PropertyInfo property = properties.Get(i);
            
            Completion completion = new Completion(property.Name, property.Name, CompletionEntryType::ClassProperty);
            Completion.DocHtml = '<span>' + property.PropertyType.Name + " " + property.Name + '</span>';

            completions.Add(completion);
        }

        return completions;
    }

]]></Source>
			</Method>
			<Method>
				<Name>GetIndexCompletions</Name>
				<Source><![CDATA[
    CompletionCollection GetIndexCompletions(str _tableName)
    {
        CompletionCollection completions = new CompletionCollection();

        SysDictTable dictTable = SysDictTable::newName(_tableName);

        if (!dictTable) return completions;

        for (int i = 1; i <= dictTable.indexCnt(); i++)
        {
            int indexId = dictTable.indexCnt2Id(i);
            str indexName = indexId2Name(dictTable.id(), IndexId);
            completions.Add(new Completion(indexName, indexName, CompletionEntryType::TableIndex));
        }

        return completions;
    }

]]></Source>
			</Method>
			<Method>
				<Name>GetEnumCompletions</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the enum members of an enum
    /// </summary>
    /// <param name = "_enumName">Enum name</param>
    /// <returns>Collection of enum members</returns>
    CompletionCollection GetEnumCompletions(str _enumName)
    {
        CompletionCollection completions = new CompletionCollection();
        
        SysDictEnum dictEnum = new SysDictEnum(enumName2Id(_enumName));

        if (!dictEnum) return completions;

        for (int i = 0; i < dictEnum.values(); i++)
        {
            str symbol = dictEnum.index2Symbol(i);
            completions.Add(new Completion(symbol, symbol, CompletionEntryType::EnumValue));
        }

        return completions;
    }

]]></Source>
			</Method>
			<Method>
				<Name>stringEnumeratorToList</Name>
				<Source><![CDATA[
    /// <summary>
    /// Converts a string enumerator into a list
    /// </summary>
    /// <param name = "_enumerator">String enumerator</param>
    /// <returns>A <c>List</c></returns>
    private static List stringEnumeratorToList(System.Collections.Specialized.StringEnumerator _enumerator)
    {
        List list = new List(Types::String);

        while (_enumerator.MoveNext())
        {
            list.addEnd(_enumerator.Current);
        }

        return list;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getClassNames</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the list of classes
    /// </summary>
    /// <returns>List of classes</returns>
    public static List getClassNames()
    {
        if (classNames)
        {
            return classNames;
        }

        classNames = MXTXppCompletionProvider::stringEnumeratorToList(Microsoft.Dynamics.Ax.Xpp.MetadataSupport::ClassNames());

        return classNames;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getTableNames</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the list of tables
    /// </summary>
    /// <returns>List of tables</returns>
    public static List getTableNames()
    {
        if (tableNames)
        {
            return tableNames;
        }

        tableNames = MXTXppCompletionProvider::stringEnumeratorToList(Microsoft.Dynamics.Ax.Xpp.MetadataSupport::TableNames());

        return tableNames;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getEDTNames</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the list of extended data types
    /// </summary>
    /// <returns>List of extended data types</returns>
    public static List getEDTNames()
    {
        if (edtNames)
        {
            return edtNames;
        }

        edtNames = MXTXppCompletionProvider::stringEnumeratorToList(Microsoft.Dynamics.Ax.Xpp.MetadataSupport::EdtNames());

        return edtNames;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getEnumNames</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the list of enums
    /// </summary>
    /// <returns>List of enums</returns>
    public static List getEnumNames()
    {
        if (enumNames)
        {
            return enumNames;
        }

        enumNames = MXTXppCompletionProvider::stringEnumeratorToList(Microsoft.Dynamics.Ax.Xpp.MetadataSupport::EnumNames());

        return enumNames;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getGlobalFunctions</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the list of global and predefined functions
    /// </summary>
    /// <returns>List of global functions</returns>
    public static List getGlobalFunctions()
    {
        if (globalFunctions)
        {
            return globalFunctions;
        }

        globalFunctions = new List(Types::String);

        MXTXppCompletionProvider::fillClassMethodList(globalFunctions, classStr(Global), true, false);
        MXTXppCompletionProvider::fillClassMethodList(globalFunctions, classStr(MXTXppPredefinedFunctions), true, false);
        
        return globalFunctions;
    }

]]></Source>
			</Method>
			<Method>
				<Name>fillClassMethodList</Name>
				<Source><![CDATA[
    /// <summary>
    /// Fills the given list with the class methods
    /// </summary>
    /// <param name = "_list">List to fill</param>
    /// <param name = "_className">Class name</param>
    /// <param name = "_static">If static methods are requested</param>
    /// <param name = "_instance">If instance methods are requested</param>
    private static void fillClassMethodList(List _list, ClassName _className, boolean _static, boolean _instance)
    {
        SysDictClass  globalDict = SysDictClass::newName(_className);
        SetEnumerator globalMethodsEnumerator = globalDict.methods(_instance, _static, false).getEnumerator();
        
        while (globalMethodsEnumerator.moveNext())
        {
            SysDictMethod method = globalMethodsEnumerator.current();

            _list.addEnd(method.name());
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>fillMethodCompletions</Name>
				<Source><![CDATA[
    /// <summary>
    /// Fills the given completion collection with the SysDictMethod set
    /// </summary>
    /// <param name = "_methods">Set of SysDictMethod</param>
    /// <param name = "_isTable">If the request is for a table</param>
    /// <param name = "_completions">Completions to fill</param>
    private void fillMethodCompletions(Set _methods, boolean _isTable, CompletionCollection _completions)
    {
        SetEnumerator enumerator = _methods.getEnumerator();

        while (enumerator.moveNext())
        {
            SysDictMethod method = enumerator.current() as SysDictMethod;

            Completion completion = new Completion(method.name(), method.name(), method.isStatic() ? CompletionEntryType::StaticMethod : CompletionEntryType::Method);
            completion.DocHtml = '<span>' + this.getMethodDocumentation(method) + '</span>';

            _completions.Add(completion);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>fillNetMethodCompletions</Name>
				<Source><![CDATA[
    private void fillNetMethodCompletions(System.Type _type, boolean _isStatic, CompletionCollection _completions)
    {
        Reflection.BindingFlags flags = Reflection.BindingFlags::Public;

        flags = flags | (_isStatic ? Reflection.BindingFlags::Static : Reflection.BindingFlags::Instance);

        Reflection.MethodInfo[] methods = _type.GetMethods(flags);

        for (int i = 0; i < methods.Length; i++)
        {
            Reflection.MethodInfo methodInfo = methods.Get(i);
            
            Completion completion = new Completion(methodInfo.Name, methodInfo.Name, methodInfo.IsStatic ? CompletionEntryType::StaticMethod : CompletionEntryType::Method);
            _completions.Add(completion);
        }

    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>