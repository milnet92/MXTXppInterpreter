<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>MXTXppCastingProxy</Name>
	<SourceCode>
		<Declaration><![CDATA[
using XppInterpreter.Interpreter.Proxy;
using Microsoft.Dynamics.Ax;


/// <summary>
/// Cast proxy used by the interpreter
/// </summary>
internal final class MXTXppCastingProxy implements XppInterpreter.Interpreter.Proxy.IXppCastingProxy
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>getXppTypeByTypeName</Name>
				<Source><![CDATA[
    /// <summary>
    /// Retrieve the Types type by name
    /// </summary>
    /// <param name = "_name">Type name</param>
    /// <returns>Types type</returns>
    internal static Types getXppTypeByTypeName(str _name)
    {
        // Base Enums
        if (SysDictEnum::newName(_name) != null)
        {
            return Types::Enum;
        }

        // Extended data types
        var dictType = SysDictType::newName(_name);
        if (dictType != null)
        {
            return dictType.baseType();
        }

        //Common
        if (SysDictTable::newName(_name) != null)
        {
            return Types::Record;
        }

        // Classes
        if (SysDictClass::newName(_name) != null)
        {
            return Types::Class;
        }

        switch(_name)
        {
            case MXtXppTypes::RealTypeStr: return Types::Real;
            case MXtXppTypes::Int32TypeStr: return Types::Integer;
            case MXtXppTypes::GuidTypeStr: return Types::Guid;
            case MXtXppTypes::ContainerTypeStr: return Types::Container;
            case MXtXppTypes::Int64TypeStr: return Types::Int64;
            case MXtXppTypes::DateTimeTypeStr: return Types::UtcDateTime;
            case MXtXppTypes::StringTypeStr: return Types::String;
            case MXtXppTypes::DateTypeStr : return Types::Date;
            case MXtXppTypes::AnyTypeTypeStr: return Types::AnyType;
        }

        return Types::void;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getSystemTypeFromTypes</Name>
				<Source><![CDATA[
    public static System.Type getSystemTypeFromTypes(Types _type)
    {
        switch (_type)
        {
            case Types::Integer:
                return MXTXppTypes::Int32Type;

            case Types::Int64:
                return MXTXppTypes::Int64Type;

            case Types::Guid:
                return MXTXppTypes::GuidType;

            case Types::Real:
                return MXTXppTypes::DecimalType;

            case Types::String:
                return MXTXppTypes::StringType;

            case Types::UtcDateTime:
                return MXTXppTypes::DateTimeType;

            case Types::Container:
                return MXTXppTypes::ContainerType;

            case Types::Date:
                return MXTXppTypes::DateType;

            case Types::AnyType:
                return MXTXppTypes::AnyTypeType;
        }

        return null;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getClrTypeFromTypeName</Name>
				<Source><![CDATA[
    public static System.Type getClrTypeFromTypeName(str _typeName)
    {
        // boolean is not a type for some reason
        boolean isBoolean = _typeName == MXtXppTypes::BooleanTypeStr;

        if (isBoolean)
            return MXTXppTypes::BooleanType;

        var xppType = MXTXppCastingProxy::getXppTypeByTypeName(_typeName);

        switch (xppType)
        {
            case Types::Class:
                return Microsoft.Dynamics.Ax.Xpp.ReflectionCallHelper::GetTypeFromClassName(_typeName);

            case Types::Enum:
                str enumName = _typeName;
                if (extendedTypeName2Id(_typeName))
                {
                    enumName = enumId2Name(SysDictType::newName(_typeName).enumId());
                }
                return Microsoft.Dynamics.Ax.Xpp.ReflectionCallHelper::getType(enumName);

            case Types::Record:
                return Microsoft.Dynamics.Ax.Xpp.ReflectionCallHelper::getType(_typeName);

            default:
                return MXTXppCastingProxy::getSystemTypeFromTypes(xppType);
        }

        return null;
    }

]]></Source>
			</Method>
			<Method>
				<Name>GetSystemTypeFromTypeName</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the System.Type by native type name (str, int, boolean...)
    /// </summary>
    /// <param name = "_typeName">Native type name</param>
    /// <returns></returns>
    public System.Type GetSystemTypeFromTypeName(str _typeName)
    {
        return MXTXppCastingProxy::getClrTypeFromTypeName(_typeName);
    }

]]></Source>
			</Method>
			<Method>
				<Name>ToBoolean</Name>
				<Source><![CDATA[
    /// <summary>
    /// Casts an object into a boolean
    /// </summary>
    /// <param name = "value"></param>
    /// <returns></returns>
	public boolean ToBoolean(System.Object value)
    {
        return value;
	}

]]></Source>
			</Method>
			<Method>
				<Name>CreateDynamicArray</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates a native dynamic array of the given type as string
    /// </summary>
    /// <param name = "typeName">Array type</param>
    /// <returns>Dynamics array as an object</returns>
    public System.Object CreateDynamicArray(System.String typeName)
    {
        System.Type type = this.GetSystemTypeFromTypeName(typeName);

        return XppInterpreter.Core.ReflectionHelper::MakeGenericInstance(MXTXppTypes::ArrayType, type);
    }

]]></Source>
			</Method>
			<Method>
				<Name>CreateFixedArray</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates a native fixed sized array of the given type as string
    /// </summary>
    /// <param name = "typeName">Array type</param>
    /// <param name = "size">Array size</param>
    /// <returns>Fixed size array as an object</returns>
    public System.Object CreateFixedArray(System.String typeName, System.Int32 size)
    {
        System.Type type = this.GetSystemTypeFromTypeName(typeName);

        return XppInterpreter.Core.ReflectionHelper::MakeGenericInstance(MXTXppTypes::ArrayType, type, size);
    }

]]></Source>
			</Method>
			<Method>
				<Name>GetArrayIndexValue</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the value of an array by index performing type and boundary checks
    /// </summary>
    /// <param name = "array">Array object</param>
    /// <param name = "idx">Index</param>
    /// <returns>Value</returns>
    public System.Object GetArrayIndexValue(System.Object array, System.Int32 idx)
    {
        if (!array.GetType().IsGenericType || array.GetType().GetGenericTypeDefinition() != MXTXppTypes::ArrayType)
        {
            throw Error("The array indexing can only be applied to X++ array types.");
        }
        else
        {
            return XppInterpreter.Core.ReflectionHelper::GetEdtArrayIndexerValue(array, idx);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>SetArrayIndexValue</Name>
				<Source><![CDATA[
    /// <summary>
    /// Sets the value of an array by index performing type and boundary checks
    /// </summary>
    /// <param name = "array">Array object</param>
    /// <param name = "idx">Index</param>
    /// <param name = "value">Value</param>
    public void SetArrayIndexValue(System.Object array, System.Int32 idx, System.Object value)
    {
        if (!array.GetType().IsGenericType || array.GetType().GetGenericTypeDefinition() != MXTXppTypes::ArrayType)
        {
            throw Error("The array indexing can only be applied to X++ array types.");
        }
        else
        {
            XppInterpreter.Core.ReflectionHelper::SetEdtArrayIndexerValue(array, idx, value);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>As</Name>
				<Source><![CDATA[
    /// <summary>
    /// Executes the as statement
    /// </summary>
    /// <param name = "_value">Value to cast</param>
    /// <param name = "_typeName">Type cast</param>
    /// <returns></returns>
    public System.Object As(System.Object _value, System.String _typeName)
    {
        if (this.Is(_value, _typeName))
        {
            return _value;
        }

        return null;
    }

]]></Source>
			</Method>
			<Method>
				<Name>Is</Name>
				<Source><![CDATA[
    /// <summary>
    /// Executes the is statement
    /// </summary>
    /// <param name = "_value">Value to check</param>
    /// <param name = "_typeName">Type name to check</param>
    /// <returns></returns>
    public System.Boolean Is(System.Object _value, System.String _typeName)
    {
        if (className2Id(_typeName))
        {
            ClassId         classId,
                            newClassId = className2Id(_typeName);
            SysDictClass    sysDictClass;
            boolean         ret;
            
            classId = classIdGet(_value);
            if (classId == 0)
            {
                return false;
            }

            sysDictClass = new SysDictClass(classId);

            ret = sysDictClass.isImplementing(newClassId);

            if (!ret)
            {
                ret = SysDictClass::isEqualOrSuperclass(classId, newClassId);
            }

            return ret;
        }
        else if (tableName2Id(_typeName))
        {
            // Validate if it's common'
            if (_value)
            {
                System.Type valueType = _value.GetType();
                boolean isCommonType = valueType == MXTXppTypes::CommonType || valueType.IsSubclassOf(MXTXppTypes::CommonType);

                if (!isCommonType)
                {
                    return false;
                }
                else if (_typeName == 'Common')
                {
                    return true;
                }
            }

            // Validate extends
            TableId  sourceTableId = tableName2Id(_value.GetType().Name);
            TableId  targetTableId = tablename2id(_typeName);
            
            if (sourceTableId == targetTableId)
            {
                return true;
            }

            while (sourceTableId)
            {
                DictTable dt = new DictTable(sourceTableId);

                if (dt.extends() != targetTableId)
                {
                    sourceTableId = dt.extends();
                }
                else
                {
                    return true;
                }
            }
        }

        return false;
    }

]]></Source>
			</Method>
			<Method>
				<Name>GetDefaultValueForSystemType</Name>
				<Source><![CDATA[
    public System.Object GetDefaultValueForSystemType(System.Type _type)
    {
        switch (_type)
        {
            case MXTXppTypes::BooleanType: return this.GetDefaultValueForType(MXTXppTypes::BooleanTypeStr);
            case MXTXppTypes::DateType: return this.GetDefaultValueForType(MXTXppTypes::DateTypeStr);
            case MXTXppTypes::DateTimeType: return this.GetDefaultValueForType(MXTXppTypes::DateTimeTypeStr);
            case MXTXppTypes::StringType: return this.GetDefaultValueForType(MXTXppTypes::StringTypeStr);
            case MXTXppTypes::Int32Type: return this.GetDefaultValueForType(MXTXppTypes::Int32TypeStr);
            case MXTXppTypes::Int64Type: return this.GetDefaultValueForType(MXTXppTypes::Int64TypeStr);
            case MXTXppTypes::GuidType: return this.GetDefaultValueForType(MXTXppTypes::GuidTypeStr);
            case MXTXppTypes::DecimalType: return this.GetDefaultValueForType(MXTXppTypes::RealTypeStr);
            default: return this.GetDefaultValueForType(_type.Name);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>GetDefaultValueForType</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the default value for the given type name (int: 0, boolean: false ...)
    /// </summary>
    /// <param name = "_name">Type name</param>
    /// <returns></returns>
	public System.Object GetDefaultValueForType(System.String _name)
    {
        if (_name == MXTXppTypes::BooleanTypeStr) return false;

        Types types = MXTXppCastingProxy::getXppTypeByTypeName(_name);
        
        switch (types)
        {
            case Types::Integer: return 0;
            case Types::Real: return 0.0;
            case Types::Container: return conNull();
            case Types::Date: return dateNull();
            case Types::Guid: return emptyGuid();
            case Types::UtcDateTime: return DateTimeUtil::minValue();
            case Types::Int64:
                {
                    int64 val = 0;
                    return val;
                }
            case Types::String: return "";
            case Types::Record: return DictTable::createRecord(_name);
            case Types::Enum: return SysDictEnum::newName(_name).firstValue();
        }

        return null;
	}

]]></Source>
			</Method>
			<Method>
				<Name>isEnumType</Name>
				<Source><![CDATA[
    /// <summary>
    /// Checks if the given System.Type is an enumeration
    /// </summary>
    /// <param name = "_type">Systen.Type to check</param>
    /// <returns>True if it is an enum, otherwise false</returns>
    private boolean isEnumType(System.Type _type)
    {
        return SysDictEnum::newName(_type.Name) != null;
    }

]]></Source>
			</Method>
			<Method>
				<Name>ImplicitConversionExists</Name>
				<Source><![CDATA[
    public System.Boolean ImplicitConversionExists(System.Type _fromType, System.Type _toType)
    {
        if (_toType == null ) return false;
        if (_fromType == _toType) return true;
        if (_toType.IsAssignableFrom(_fromType) || _fromType.IsAssignableFrom(_toType)) return true;

        // anytype is always convertible
        if (_toType == MXTXppTypes::AnyTypeType) return true;

        // int, int64, real, boolean and enum are all implicitly convertible
        Set numCheckSet = new Set(Types::Class);
        numCheckSet.add(MXTXppTypes::Int32Type);
        numCheckSet.add(MXTXppTypes::Int64Type);
        numCheckSet.add(MXTXppTypes::DecimalType);
        numCheckSet.add(MXTXppTypes::BooleanType);

        if ((numCheckSet.in(_fromType) || this.isEnumType(_fromType)) &&
            (numCheckSet.in(_toType) || this.isEnumType(_toType)))
        {
            return true;
        }

        // date and datetime are convertible
        if ((_toType == MXTXppTypes::DateType && _fromType == MXTXppTypes::DateTimeType) ||
            (_toType == MXTXppTypes::DateTimeType && _fromType == MXTXppTypes::DateType))
        {
            return true;
        }

        return false;
    }

]]></Source>
			</Method>
			<Method>
				<Name>IsReferenceType</Name>
				<Source><![CDATA[
    /// <summary>
    /// Checks if the System.Type provided is a reference type
    /// </summary>
    /// <param name = "_type">System.Type</param>
    /// <returns>True if it's a reference type, otherwise false</returns>
    public System.Boolean IsReferenceType(System.Type _type)
    {
        if (_type == null) return false;

        switch (_type)
        {
            case MXTXppTypes::AnyTypeType:
            case MXTXppTypes::ArrayType:
            case MXTXppTypes::BooleanType:
            case MXTXppTypes::CommonType:
            case MXTXppTypes::ContainerType:
            case MXTXppTypes::DateTimeType:
            case MXTXppTypes::DateType:
            case MXTXppTypes::DecimalType:
            case MXTXppTypes::GuidType:
            case MXTXppTypes::Int32Type:
            case MXTXppTypes::Int64Type:
            case MXTXppTypes::StringType:
                return false;
        }

        Types type = MXTXppCastingProxy::getXppTypeByTypeName(_type.Name);

        switch (type)
        {
            case Types::BLOB:
            case Types::Class:
            case Types::UserType:
            case Types::VarString:
            case Types::VarArg:
            case Types::void:
                return true;
        }

        return false;
    }

]]></Source>
			</Method>
			<Method>
				<Name>Cast</Name>
				<Source><![CDATA[
    public System.Object Cast(System.Object _value, System.Type _toType)
    {
        if (_value != null)
        {
            System.Type fromType = _value.GetType();

            if (fromType == _toType || _toType.IsAssignableFrom(fromType) || fromType.IsAssignableFrom(_toType))
            {
                return _value;
            }
        }

        boolean isCommonType = _toType == MXTXppTypes::CommonType || _toType.IsSubclassOf(MXTXppTypes::CommonType);
        
        if (this.isEnumType(_toType))
        {
            return Microsoft.Dynamics.Ax.Xpp.CastHelper::ObjectToInt(_value);
        }

        switch (_toType)
        {
            case MXTXppTypes::BooleanType: return Microsoft.Dynamics.Ax.Xpp.CastHelper::ToBoolean(_value);
            case MXTXppTypes::DateType: return Microsoft.Dynamics.Ax.Xpp.CastHelper::ObjectToDate(_value, false);
            case MXTXppTypes::DateTimeType: return Microsoft.Dynamics.Ax.Xpp.CastHelper::ObjectToDateTime(_value, false);
            case MXTXppTypes::DecimalType: return Microsoft.Dynamics.Ax.Xpp.CastHelper::ObjectToReal(_value);
            case MXTXppTypes::GuidType: return Microsoft.Dynamics.Ax.Xpp.CastHelper::ObjectToGuid(_value);
            case MXTXppTypes::Int32Type: return Microsoft.Dynamics.Ax.Xpp.CastHelper::ObjectToInt(_value);
            case MXTXppTypes::Int64Type: return Microsoft.Dynamics.Ax.Xpp.CastHelper::ObjectToInt64(_value);
            case MXTXppTypes::StringType: return Microsoft.Dynamics.Ax.Xpp.CastHelper::ObjectToString(_value);
        }

        return System.Convert::ChangeType(_value, _toType);
    }

]]></Source>
			</Method>
			<Method>
				<Name>GetArrayType</Name>
				<Source><![CDATA[
    public System.Type GetArrayType(System.Type _type)
    {
        if (_type == null) return null;

        return MXTXppTypes::ArrayType.MakeGenericType(_type);
    }

]]></Source>
			</Method>
			<Method>
				<Name>CreateDate</Name>
				<Source><![CDATA[
    public System.Object CreateDate(System.Int32 day, System.Int32 month, System.Int32 year)
    {
        return mkDate(day, month, year);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>