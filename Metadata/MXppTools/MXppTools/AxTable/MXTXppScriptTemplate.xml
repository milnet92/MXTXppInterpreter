<?xml version="1.0" encoding="utf-8"?>
<AxTable xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>MXTXppScriptTemplate</Name>
	<SourceCode>
		<Declaration><![CDATA[
public class MXTXppScriptTemplate extends common
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>find</Name>
				<Source><![CDATA[
    public static MXTXppScriptTemplate find(MXTTemplateId _templateId, boolean _forUpdate = false)
    {
        MXTXppScriptTemplate template;

        if (_templateId)
        {
            select firstonly template
                index TemplateIdx
                where template.TemplateId == _templateId;

            if (_forUpdate)
            {
                template.selectForUpdate(true);
            }
        }

        return template;
    }

]]></Source>
			</Method>
			<Method>
				<Name>exist</Name>
				<Source><![CDATA[
    public static boolean exist(MXTTemplateId _templateId)
    {
        if (_templateId)
        {
            return (select firstonly RecId from MXTXppScriptTemplate
                        index TemplateIdx
                        where MXTXppScriptTemplate.TemplateId == _templateId).RecId != 0;
        }

        return false;
    }

]]></Source>
			</Method>
			<Method>
				<Name>applyTemplateMap</Name>
				<Source><![CDATA[
    /// <summary>
    ///
    /// </summary>
    public static MXTSourceCode applyTemplateMap(MXTTemplateId _templateId, MXTXppTmpScriptTemplateMap _mapping)
    {
        MXTXppScriptTemplate template = MXTXppScriptTemplate::find(_templateId);
        MXTSourceCode        sourceCode = template.SourceCode;

        if (template)
        {
            while select _mapping
            {
                sourceCode = strReplace(sourceCode, _mapping.Placeholder, _mapping.Value);
            }
        }

        return sourceCode;
    }

]]></Source>
			</Method>
			<Method>
				<Name>generateTemplateFromEventHandler</Name>
				<Source><![CDATA[
    /// <summary>
    ///
    /// </summary>
    public static MXTSourceCode generateTemplateFromEventHandler(MXTXppEventHandlerTable _eventHandler)
    {
        MXTSourceCode code = @"%1
void %2_%3(%4)
{

}
";

        System.Type elementType = _eventHandler.getElementType();
        if (elementType == null)
        {
            throw error(strFmt("Invalid element name %1", _eventHandler.ElementName));
        }

        System.Type delegateType = _eventHandler.getDelegateType();
        if (delegateType == null)
        {
            throw error(strFmt("Invalid method name %1", _eventHandler.MethodName));
        }

        System.Reflection.ParameterInfo[] parameters = delegateType.GetMethod("Invoke").GetParameters();
        
        str parameterCode = "";
        for (int i = 0; i < parameters.Length; i++)
        {
            System.Reflection.ParameterInfo parameter = parameters.Get(i);
            System.Type parameterType = parameter.ParameterType;

            if (i != 0)
            {
                parameterCode += ", ";
            }

            parameterCode += parameterType.Name;
            parameterCode += ' ';
            parameterCode += parameter.Name == null ? strFmt("_parm%1", i) : strFmt("_%1", parameter.Name);
        }

        str methodName = _eventHandler.DisplayMethodName;
        
        if (_eventHandler.HandlerType == MXTXppEventHandlerType::DataEvent && strStartsWith(methodName, "On"))
        {
            methodName = "o" + subStr(methodName, 2, strLen(methodName) - 1);
        }

        code = strFmt(code, 
            MXTXppScriptTemplate::generateEventHandlerDecoration(_eventHandler),
            _eventHandler.ElementName, 
            methodName, 
            parameterCode);

        return code;
    }

]]></Source>
			</Method>
			<Method>
				<Name>generateEventHandlerDecoration</Name>
				<Source><![CDATA[
    public static str generateEventHandlerDecoration(MXTXppEventHandlerTable _eventHandler)
    {
        str getMethodFormatted(MXTXppEventHandlerTable _table)
        {
            str methodName = _table.DisplayMethodName;

            methodName = strReplace(methodName, "_PreDelegate", "");
            methodName = strReplace(methodName, "_PostDelegate", "");
            methodName = strReplace(methodName, "$", "");

            if (_table.HandlerType == MXTXppEventHandlerType::DataEvent && strStartsWith(methodName, "On"))
                methodName = "o" + subStr(methodName, 2, strLen(methodName) - 1);

            return methodName;
        }

        str getMethodElementIdentifier(MXTXppEventHandlerTable _table)
        {
            switch (_table.ElementType)
            {
                case MXTXppEventElementType::Class: return "classStr";
                case MXTXppEventElementType::Table: return "tableStr";
                case MXTXppEventElementType::Form: return "formStr";
                default: return "";
            }
        }

        str getMethodEventIdentifier(MXTXppEventHandlerTable _table)
        {
            str methodName = getMethodFormatted(_table);

            if (_table.HandlerType == MXTXppEventHandlerType::Static)
                return strFmt("staticDelegateStr(%1, %2)", _table.ElementName, methodName);
            
            switch (_table.ElementType)
            {
                case MXTXppEventElementType::Class: 
                    return strFmt("methodStr(%1, %2", _table.ElementName, methodName);
                case MXTXppEventElementType::Table:
                    return strFmt("tableMethodStr(%1, %2", _table.ElementName, methodName);
                case MXTXppEventElementType::Form:
                    return strFmt("formMethod(%1, %2", _table.ElementName, methodName);
                default: return "";
            }
        }


        str decoration = @"/// <summary>
/// Event handler for %1.%2
/// Native X++ Attribute: %3
/// </summary>";

        str elementMethod;
        str elementIdentifier;
        str eventIdentifier;

        switch (_eventHandler.HandlerType)
        {
            case MXTXppEventHandlerType::DataEvent: 
                elementMethod = "DataEventHandler";
                elementIdentifier = strFmt("tableStr(%1)", _eventHandler.ElementName);
                eventIdentifier = strFmt("DataEventType::%1",MXTXppEventHandlerManager::getDataEventTypeFromName(_eventHandler.MethodName));
                break;
            case MXTXppEventHandlerType::Pre:
                elementMethod = "PreHandlerFor";
                elementIdentifier = strFmt("%1(%2)", getMethodElementIdentifier(_eventHandler), _eventHandler.ElementName);
                eventIdentifier = getMethodEventIdentifier(_eventHandler);
                break;
            case MXTXppEventHandlerType::Post: 
                elementMethod = "PostHandlerFor";
                elementIdentifier = strFmt("%1(%2)", getMethodElementIdentifier(_eventHandler), _eventHandler.ElementName);
                eventIdentifier = getMethodEventIdentifier(_eventHandler);
                break;
            case MXTXppEventHandlerType::Static: 
                elementMethod = "SubscribesTo";
                elementIdentifier = strFmt("%1(%2)", getMethodElementIdentifier(_eventHandler), _eventHandler.ElementName);
                eventIdentifier = getMethodEventIdentifier(_eventHandler);
                break;
        }

        decoration = strFmt(
            decoration, 
            _eventHandler.ElementName,
            getMethodFormatted(_eventHandler),
            strFmt("[%1(%2, %3)]", elementMethod, elementIdentifier, eventIdentifier));

        return decoration;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
	<SubscriberAccessLevel>
		<Read>Allow</Read>
	</SubscriberAccessLevel>
	<AllowRowVersionChangeTracking>Yes</AllowRowVersionChangeTracking>
	<SaveDataPerCompany>No</SaveDataPerCompany>
	<DeleteActions>
		<AxTableDeleteAction>
			<Name>MXTXppScriptTemplateMap</Name>
			<Relation>MXTXppScriptTemplate</Relation>
			<Table>MXTXppScriptTemplateMap</Table>
			<Tags></Tags>
		</AxTableDeleteAction>
	</DeleteActions>
	<FieldGroups>
		<AxTableFieldGroup>
			<Name>AutoReport</Name>
			<Fields />
		</AxTableFieldGroup>
		<AxTableFieldGroup>
			<Name>AutoLookup</Name>
			<Fields />
		</AxTableFieldGroup>
		<AxTableFieldGroup>
			<Name>AutoIdentification</Name>
			<AutoPopulate>Yes</AutoPopulate>
			<Fields />
		</AxTableFieldGroup>
		<AxTableFieldGroup>
			<Name>AutoSummary</Name>
			<Fields />
		</AxTableFieldGroup>
		<AxTableFieldGroup>
			<Name>AutoBrowse</Name>
			<Fields />
		</AxTableFieldGroup>
	</FieldGroups>
	<Fields>
		<AxTableField xmlns=""
			i:type="AxTableFieldString">
			<Name>TemplateId</Name>
			<AllowEdit>No</AllowEdit>
			<ExtendedDataType>MXTTemplateId</ExtendedDataType>
			<IgnoreEDTRelation>Yes</IgnoreEDTRelation>
			<Mandatory>Yes</Mandatory>
		</AxTableField>
		<AxTableField xmlns=""
			i:type="AxTableFieldString">
			<Name>SourceCode</Name>
			<ExtendedDataType>MXTSourceCode</ExtendedDataType>
			<IgnoreEDTRelation>Yes</IgnoreEDTRelation>
		</AxTableField>
		<AxTableField xmlns=""
			i:type="AxTableFieldString">
			<Name>Description</Name>
			<ExtendedDataType>Description255</ExtendedDataType>
			<IgnoreEDTRelation>Yes</IgnoreEDTRelation>
		</AxTableField>
	</Fields>
	<FullTextIndexes />
	<Indexes>
		<AxTableIndex>
			<Name>TemplateIdx</Name>
			<AlternateKey>Yes</AlternateKey>
			<Fields>
				<AxTableIndexField>
					<DataField>TemplateId</DataField>
				</AxTableIndexField>
			</Fields>
		</AxTableIndex>
	</Indexes>
	<Mappings />
	<Relations />
	<StateMachines />
</AxTable>